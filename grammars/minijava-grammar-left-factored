<Inicial> ::= <ListaClases>
<ListaClases> ::= <Clase> <OtrasClases>
<OtrasClases> ::= <ListaClases> | EOF
<Clase> ::= class idClase <Herencia> { <ListaMiembros> }
<Herencia> ::= extends idClase | EOF
<ListaMiembros> ::= <Miembro> <ListaMiembros> | EOF
<Miembro> ::= <Atributo> | <Constructor> | <Metodo>
<Atributo> ::= <Visibilidad> <Tipo> <ListaNombreAtributo> ;
<Metodo> ::= <FormaMetodo> <TipoMetodo> idMetVar <ArgsFormales> <Bloque>
<Constructor> ::= idClase <ArgsFormales> <Bloque>
<Visibilidad> ::= public | private
<Tipo> ::= <TipoPrimitivo> | idClase
<TipoPrimitivo> ::= boolean | char | int | String
<ListaNombreAtributo> ::= idMetVar <OtrosNombreAtributo>
<OtrosNombreAtributo> ::= , <ListaNombreAtributo> | EOF
<FormaMetodo> ::= static | dynamic
<TipoMetodo> ::= <Tipo> | void
<ArgsFormales> ::= ( <ListaArgsFormalesOVacio> )
<ListaArgsFormalesOVacio> ::= <ListaArgsFormales> | EOF
<ListaArgsFormales> ::= <ArgFormal> <OtrosArgsFormales>
<OtrosArgsFormales> ::= , <ListaArgsFormales> | EOF
<ArgFormal> ::= <Tipo> idMetVar
<Bloque> ::= { <ListaSentencias> }
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | EOF
<Sentencia> ::= ;
<Sentencia> ::= <LlamadaOAsignacion> ;
<Sentencia> ::= <Llamada> ;
<Sentencia> ::= <Tipo> <ListaDecVars> ;
<Sentencia> ::= if ( <Expresion> ) <Sentencia> <ElseOVacio>
<ElseOVacio> ::= else <Sentencia>| EOF
<Sentencia> ::= while ( <Expresion> ) <Sentencia>
<Sentencia> ::= <Bloque>
<Sentencia> ::= return <ExpresionOVacio> ;
<LlamadaOAsignacion> ::= <Acceso> <AsignacionOVacio> ;
<AsignacionOVacio> ::= <TipoDeAsignacion> <Expresion> | EOF
<TipoDeAsignacion> ::= = | += | -=
<ListaDecVars> ::= idMetVar <OtrosVars>
<OtrosVars> ::= , <ListaDecVars> | EOF
<ExpresionOVacio> ::= <Expresion> | EOF
<Expresion> ::= <ExpresionUnaria> <ExpresionResto>
<ExpresionResto> ::= <OperadorBinario> <ExpresionUnaria> <ExpresionResto> | EOF
<OperadorBinario> ::= || | && | == | != | < | > | <= | >= | + | - | * | / | %
<ExpresionUnaria> ::= <OperadorUnario> <Operando>
<ExpresionUnaria> ::= <Operando>
<OperadorUnario> ::= + | - | !
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= null | true | false | intLiteral | charLiteral | stringLiteral
<Acceso> ::= <Primario> <Encadenado>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoEstatico>
<Primario> ::= <AccesoConstructor>
<Primario> ::= <AccesoVarOMetodo>
<Primario> ::= ( <Expresion> )
<AccesoThis> ::= this
<AccesoEstatico> ::= static idClase . <AccesoMetodo>
<AccesoConstructor> ::= new idClase <ArgsActuales>
<AccesoVarOMetodo> ::= idMetVar <ArgsOVacio>
<ArgsOVacio> ::= <ArgsActuales> | EOF
<ArgsActuales> ::= ( <ListaExpsOVacio> )
<ListaExpsOVacio> ::= <ListaExps> | EOF
<ListaExps> ::= <Expresion> <OtrosListaExps>
<OtrosListaExps> ::= , <ListaExps> | EOF
<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado> | EOF
<VarOMetodoEncadenado> ::= . idMetVar <ArgsOVacio>