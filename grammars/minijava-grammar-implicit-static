<Inicial> ::= <ListaClasesInterfaces>
<ListaClasesInterfaces> ::= <Clase> <OtrasClasesInterfaces>
<ListaClasesInterfaces> ::= <Interfaz> <OtrasClasesInterfaces>
<OtrasClasesInterfaces> ::= <ListaClasesInterfaces> | EOF
<Clase> ::= class <TipoClaseGen> <HerenciaClase> { <ListaMiembrosClase> }
<HerenciaClase> ::= <ExtendsOVacio> <ImplementsOVacio>
<ExtendsOVacio> ::= extends <TipoClaseGen> | EOF
<ImplementsOVacio> ::= implements <TipoClaseGen> <OtrosImplements> | EOF
<OtrosImplements> ::= , <TipoClaseGen> <OtrosImplements> | EOF
<ListaMiembrosClase> ::= <MiembroClase> <ListaMiembrosClase> | EOF
<Interfaz> ::= interface <TipoClaseGen> <HerenciaInterfaz> { <ListaMiembrosInterfaz> }
<HerenciaInterfaz> ::= extends <TipoClaseGen> <OtrosHerenciaInterfaz> | EOF
<OtrosHerenciaInterfaz> ::= , <TipoClaseGen> <OtrosHerenciaInterfaz> | EOF
<ListaMiembrosInterfaz> ::= <MetodoInterfaz> <ListaMiembrosInterfaz> | EOF
<MetodoInterfaz> ::= <FormaMetodo> <TipoMetodo> idMetVar <ArgsFormales> ;
<FormaMetodo> ::= static | dynamic
<TipoMetodo> ::= <Tipo> | void



<MiembroClase> ::= <Visibilidad> <StaticOVacio> <Atributo>
<MiembroClase> ::= dynamic <Metodo>
<MiembroClase> ::= static <AtributoOMetodo>
<MiembroClase> ::= idClase <AtributoOConstructor>
<MiembroClase> ::= <TipoPrimitivo> idMetVar <RestoDecAtrs>
<Visibilidad> ::= public | private
<StaticOVacio> ::= static | EOF
<AtributoOMetodo> ::= <Tipo> idMetVar <RestoDecAtrsORestoMetodo>
<AtributoOMetodo> ::= void idMetVar <RestoMetodo>
<RestoDecAtrsORestoMetodo> ::= <RestoDecAtrs> | <RestoMetodo>
<RestoDecAtrs> ::= <AsignacionOVacio> <OtrosDecAtrs> ;
<RestoMetodo> ::= <ArgsFormales> <Bloque>
<Atributo> ::= <Tipo> <ListaDecAtrs> ;
<Metodo> ::= <TipoMetodo> idMetVar <ArgsFormales> <Bloque>
<AtributoOConstructor> ::= <GenExplicitaOVacio> <ListaDecAtrs> ;
<AtributoOConstructor> ::= <Constructor>
<Constructor> ::= <ArgsFormales> <Bloque>
<Tipo> ::= <TipoPrimitivo> | <TipoClaseGen>
<TipoPrimitivo> ::= boolean | char | int | String
<TipoClaseGen> ::= idClase <GenExplicitaOVacio>


<GenExplicitaOVacio> ::= < idClase > | EOF
<GenericidadOVacio> ::= < <RestoGenericidad> | EOF
<RestoGenericidad> ::= <GenRestoExplicito> | <GenRestoImplicito>
<GenRestoExplicito> ::= idClase >
<GenRestoImplicito> ::= >



<ListaDecAtrs> ::= idMetVar <AsignacionOVacio> <OtrosDecAtrs>
<OtrosDecAtrs> ::= , <ListaDecAtrs> | EOF
<ArgsFormales> ::= ( <ListaArgsFormalesOVacio> )
<ListaArgsFormalesOVacio> ::= <ListaArgsFormales> | EOF
<ListaArgsFormales> ::= <ArgFormal> <OtrosArgsFormales>
<OtrosArgsFormales> ::= , <ListaArgsFormales> | EOF
<ArgFormal> ::= <Tipo> idMetVar
<Bloque> ::= { <ListaSentencias> }
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | EOF
<Sentencia> ::= ;
<Sentencia> ::= <Acceso> <AsignacionOVacio> ;
<Sentencia> ::= <TipoPrimitivo> <ListaDecVars> ;
<Sentencia> ::= idClase <AccesoEstaticoOVarClase>
<Sentencia> ::= if ( <Expresion> ) <Sentencia> <ElseOVacio>
<ElseOVacio> ::= else <Sentencia> | EOF
<Sentencia> ::= while ( <Expresion> ) <Sentencia>
<Sentencia> ::= <Bloque>
<Sentencia> ::= return <ExpresionOVacio> ;
<AccesoEstaticoOVarClase> ::= . <AccesoVarOMetodo> <Encadenado> <AsignacionOVacio> ;
<AccesoEstaticoOVarClase> ::= <GenExplicitaOVacio> <ListaDecVars> ;
<AsignacionOVacio> ::= <TipoDeAsignacion> <Expresion> | EOF
<TipoDeAsignacion> ::= = | += | -=
<ListaDecVars> ::= idMetVar <AsignacionOVacio> <OtrosDecVars>
<OtrosDecVars> ::= , <ListaDecVars> | EOF
<ExpresionOVacio> ::= <Expresion> | EOF
<Expresion> ::= <ExpNivel1>
<ExpresionUnaria> ::= <OperadorUnario> <Operando>
<ExpresionUnaria> ::= <Operando>
<OperadorUnario> ::= + | - | !
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= null | true | false | intLiteral | charLiteral | stringLiteral
<Acceso> ::= <Primario> <Encadenado>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoVarOMetodo>
<Primario> ::= <AccesoEstatico>
<Primario> ::= <AccesoConstructor>
<Primario> ::= ( <Expresion> )
<AccesoThis> ::= this
<AccesoVar> ::= idMetVar
<AccesoEstatico> ::= static idClase . <AccesoVarOMetodo>
<AccesoConstructor> ::= new idClase <GenericidadOVacio> <ArgsActuales>
<AccesoMetodo> ::= idMetVar <ArgsActuales>
<AccesoVarOMetodo> ::= idMetVar <ArgsActualesOVacio>
<ArgsActualesOVacio> ::= <ArgsActuales> | EOF
<ArgsActuales> ::= ( <ListaExpsOVacio> )
<ListaExpsOVacio> ::= <ListaExps> | EOF
<ListaExps> ::= <Expresion> <OtrosExps>
<OtrosExps> ::= , <ListaExps> | EOF
<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado> | EOF
<VarOMetodoEncadenado> ::= . idMetVar <ArgsActualesOVacio>

<ExpNivel1> ::= <ExpNivel2> <ExpNivel1Resto>
<ExpNivel1Resto> ::= <OpNivel1> <ExpNivel2> <ExpNivel1Resto> | EOF
<OpNivel1> ::= == | !=

<ExpNivel2> ::= <ExpNivel3> <ExpNivel2Resto>
<ExpNivel2Resto> ::= <OpNivel2> <ExpNivel3> <ExpNivel2Resto> | EOF
<OpNivel2> ::= &&

<ExpNivel3> ::= <ExpNivel4> <ExpNivel3Resto>
<ExpNivel3Resto> ::= <OpNivel3> <ExpNivel4> <ExpNivel3Resto> | EOF
<OpNivel3> ::= ||

<ExpNivel4> ::= <ExpNivel5> <ExpNivel4Resto>
<ExpNivel4Resto> ::= <OpNivel4> <ExpNivel5> <ExpNivel4Resto> | EOF
<OpNivel4> ::= < | > | <= | >=

<ExpNivel5> ::= <ExpNivel6> <ExpNivel5Resto>
<ExpNivel5Resto> ::= <OpNivel5> <ExpNivel6> <ExpNivel5Resto> | EOF
<OpNivel5> ::= + | -

<ExpNivel6> ::= <ExpresionUnaria> <ExpNivel6Resto>
<ExpNivel6Resto> ::= <OpNivel6> <ExpresionUnaria> <ExpNivel6Resto> | EOF
<OpNivel6> ::= * | / | %