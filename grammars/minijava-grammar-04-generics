<Inicial> ::= <ListaClases>
<ListaClases> ::= <Clase> <OtrasClases>
<OtrasClases> ::= <ListaClases> | EOF
<Clase> ::= class idClase <GenExplicitaOVacio> <Herencia> { <ListaMiembros> }
<Herencia> ::= extends idClase <GenExplicitaOVacio> | EOF
<ListaMiembros> ::= <Miembro> <ListaMiembros> | EOF
<Miembro> ::= <Atributo> | <Metodo> | idClase <GenYListaAtrsOConstructor>
<Atributo> ::= <Visibilidad> <Tipo> <ListaDecAtrs> ;
<Atributo> ::= <TipoPrimitivo> <ListaDecAtrs> ;
<Metodo> ::= <FormaMetodo> <TipoMetodo> idMetVar <ArgsFormales> <Bloque>
<GenYListaAtrsOConstructor> ::= <GenExplicitaOVacio> <ListaDecAtrs> ;
<GenYListaAtrsOConstructor> ::= <RestoConstructor>
<RestoConstructor> ::= <ArgsFormales> <Bloque>
<Visibilidad> ::= public | private
<GenExplicitaOVacio> ::= < idClase > | EOF
<GenericidadOVacio> ::= < <RestoGenericidad> | EOF
<RestoGenericidad> ::= <GenRestoExplicito> | <GenRestoImplicito>
<GenRestoExplicito> ::= idClase >
<GenRestoImplicito> ::= >
<Tipo> ::= <TipoPrimitivo> | idClase <GenExplicitaOVacio>
<TipoPrimitivo> ::= boolean | char | int | String
<ListaDecAtrs> ::= idMetVar <OtrosDecAtrs>
<OtrosDecAtrs> ::= , <ListaDecAtrs> | EOF
<FormaMetodo> ::= static | dynamic
<TipoMetodo> ::= <Tipo> | void
<ArgsFormales> ::= ( <ListaArgsFormalesOVacio> )
<ListaArgsFormalesOVacio> ::= <ListaArgsFormales> | EOF
<ListaArgsFormales> ::= <ArgFormal> <OtrosArgsFormales>
<OtrosArgsFormales> ::= , <ListaArgsFormales> | EOF
<ArgFormal> ::= <Tipo> idMetVar
<Bloque> ::= { <ListaSentencias> }
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | EOF
<Sentencia> ::= ;
<Sentencia> ::= <AsignacionOLlamada> ;
<Sentencia> ::= <VarLocal> ;
<Sentencia> ::= <SentenciaReturn> ;
<Sentencia> ::= <SentenciaIf>
<Sentencia> ::= <SentenciaFor>
<Sentencia> ::= <Bloque>
<AsignacionOLlamada> ::= <Acceso> <RestoAsignacionOVacio>
<RestoAsignacionOVacio> ::= <TipoDeAsignacion> | EOF
<Asignacion> ::= <Acceso> <TipoDeAsignacion>
<TipoDeAsignacion> ::= = <Expresion> | ++ | - -
<VarLocal> ::= <Tipo> idMetVar <VarLocalAsignacionOVacio>
<VarLocalAsignacionOVacio> ::= = <Expresion> | EOF
<SentenciaReturn> ::= return <ExpresionOVacio>
<ExpresionOVacio> ::= <Expresion> | EOF
<SentenciaIf> ::= if ( <Expresion> ) <Sentencia> <ElseOVacio>
<ElseOVacio> ::= else <Sentencia> | EOF
<SentenciaFor> ::= for ( <Tipo> idMetVar <RestoFor>
<RestoFor> ::= <RestoForClasico> | <RestoForEach>
<RestoForClasico> ::= <VarLocalAsignacionOVacio> ; <Expresion> ; <Asignacion> ) <Sentencia>
<RestoForEach> ::= : <Expresion> ) <Sentencia>
<Expresion> ::= <ExpresionUnaria> <ExpresionResto>
<ExpresionResto> ::= <OperadorBinario> <ExpresionUnaria> <ExpresionResto> | EOF
<OperadorBinario> ::= || | && | == | != | < | > | <= | >= | + | - | * | / | %
<ExpresionUnaria> ::= <OperadorUnario> <Operando>
<ExpresionUnaria> ::= <Operando>
<OperadorUnario> ::= + | - | !
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= null | true | false | intLiteral | charLiteral | stringLiteral
<Acceso> ::= ( <AccesoEPoAccesoCasting> <Encadenado>
<Acceso> ::= <Primario> <Encadenado>
<AccesoEPoAccesoCasting> ::= <RestoCasting> <PrimarioOExpParen>
<AccesoEPoAccesoCasting> ::= <RestoExpresionParentizada>
<RestoCasting> ::= idClase <GenExplicitaOVacio> )
<RestoExpresionParentizada> ::= <Expresion> )
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoConstructor>
<Primario> ::= <AccesoVarOMetodo>
<PrimarioOExpParen> ::= <Primario> | <ExpresionParentizada>
<AccesoThis> ::= this
<AccesoConstructor> ::= new idClase <GenericidadOVacio> <ArgsActuales>
<ExpresionParentizada> ::= ( <Expresion> )
<AccesoVarOMetodo> ::= idMetVar <ArgsActualesOVacio>
<ArgsActualesOVacio> ::= <ArgsActuales> | EOF
<ArgsActuales> ::= ( <ListaExpsOVacio> )
<ListaExpsOVacio> ::= <ListaExps> | EOF
<ListaExps> ::= <Expresion> <OtrosExps>
<OtrosExps> ::= , <ListaExps> | EOF
<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado> | EOF
<VarOMetodoEncadenado> ::= . idMetVar <ArgsActualesOVacio>