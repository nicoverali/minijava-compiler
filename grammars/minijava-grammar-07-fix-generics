<Inicial> ::= <ListaClases>
<ListaClases> ::= <Clase> <OtrasClases>
<OtrasClases> ::= <ListaClases> | EOF
<Clase> ::= class <ClaseYGen> <Herencia> { <ListaMiembros> }
<Herencia> ::= extends <ClaseYGen> | EOF
<ListaMiembros> ::= <Miembro> <ListaMiembros> | EOF
<Miembro> ::= <Visibilidad> <StaticOVacio> <Atributo>
<Miembro> ::= dynamic <Metodo>
<Miembro> ::= static <AtributoOMetodo>
<Miembro> ::= idClase <AtributoOConstructor>
<Miembro> ::= <TipoPrimitivo> <ListaDecAtrs> ;
<AtributoOMetodo> ::= <Tipo> idMetVar <OtrosDecAtrsORestoMetodo>
<AtributoOMetodo> ::= void idMetVar <RestoMetodo>
<OtrosDecAtrsORestoMetodo> ::= <OtrosDecAtrs> ; | <RestoMetodo>
<RestoMetodo> ::= <ArgsFormales> <Bloque>
<Atributo> ::= <Tipo> <ListaDecAtrs> ;
<Metodo> ::= <TipoMetodo> idMetVar <ArgsFormales> <Bloque>
<AtributoOConstructor> ::= <GenExplicitaOVacio> <ListaDecAtrs> ;
<AtributoOConstructor> ::= <RestoConstructor>
<RestoConstructor> ::= <ArgsFormales> <Bloque>
<Visibilidad> ::= public | private
<StaticOVacio> ::= static | EOF
<ClaseYGen> ::= idClase <GenExplicitaOVacio>
<GenExplicitaOVacio> ::= < <ClaseYGen> <OtrosGen> > | EOF
<OtrosGen> ::= , <ClaseYGen> <OtrosGen> | EOF
<GenericidadOVacio> ::= < <RestoGenericidad> | EOF
<RestoGenericidad> ::= <GenRestoExplicito> | <GenRestoImplicito>
<GenRestoExplicito> ::= <ClaseYGen> <OtrosGen> >
<GenRestoImplicito> ::= >
<Tipo> ::= <TipoPrimitivo> | <ClaseYGen>
<TipoPrimitivo> ::= boolean | char | int | String
<ListaDecAtrs> ::= idMetVar <OtrosDecAtrs>
<OtrosDecAtrs> ::= , <ListaDecAtrs> | EOF
<TipoMetodo> ::= <Tipo> | void
<ArgsFormales> ::= ( <ListaArgsFormalesOVacio> )
<ListaArgsFormalesOVacio> ::= <ListaArgsFormales> | EOF
<ListaArgsFormales> ::= <ArgFormal> <OtrosArgsFormales>
<OtrosArgsFormales> ::= , <ListaArgsFormales> | EOF
<ArgFormal> ::= <Tipo> idMetVar
<Bloque> ::= { <ListaSentencias> }
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | EOF
<Sentencia> ::= ;
<Sentencia> ::= <AsignacionOLlamadaPrimitiva> ;
<Sentencia> ::= <VarLocalPrimitiva> ;
<Sentencia> ::= idClase <RestoAccesoEstaticoOVarLocalClase> ;
<Sentencia> ::= <SentenciaReturn> ;
<Sentencia> ::= <SentenciaIf>
<Sentencia> ::= <SentenciaFor>
<Sentencia> ::= <Bloque>
<AsignacionOLlamadaPrimitiva> ::= <Acceso> <RestoAsignacionOVacio>
<RestoAsignacionOVacio> ::= <TipoDeAsignacion> | EOF
<Asignacion> ::= <Acceso> <TipoDeAsignacion>
<TipoDeAsignacion> ::= = <Expresion> | ++ | - -
<VarLocalPrimitiva> ::= <TipoPrimitivo> idMetVar <VarLocalAsignacionOVacio>
<VarLocalAsignacionOVacio> ::= = <Expresion> | EOF
<RestoAccesoEstaticoOVarLocalClase> ::= <RestoAccesoEstatico> <RestoAsignacionOVacio>
<RestoAccesoEstaticoOVarLocalClase> ::= <GenExplicitaOVacio> idMetVar <VarLocalAsignacionOVacio>
<SentenciaReturn> ::= return <ExpresionOVacio>
<ExpresionOVacio> ::= <Expresion> | EOF
<SentenciaIf> ::= if ( <Expresion> ) <Sentencia> <ElseOVacio>
<ElseOVacio> ::= else <Sentencia> | EOF
<SentenciaFor> ::= for ( <Tipo> idMetVar <RestoFor>
<RestoFor> ::= <RestoForClasico> | <RestoForEach>
<RestoForClasico> ::= <VarLocalAsignacionOVacio> ; <Expresion> ; <Asignacion> ) <Sentencia>
<RestoForEach> ::= : <Expresion> ) <Sentencia>
<Expresion> ::= <ExpresionUnaria> <ExpresionResto>
<ExpresionResto> ::= <OperadorBinario> <ExpresionUnaria> <ExpresionResto> | EOF
<OperadorBinario> ::= || | && | == | != | < | > | <= | >= | + | - | * | / | %
<ExpresionUnaria> ::= <ExpresionUnariaSinStatic> | <AccesoEstatico>
<ExpresionUnariaSinStatic> ::= <OperadorUnario> <Operando>
<ExpresionUnariaSinStatic> ::= <Operando>
<OperadorUnario> ::= + | - | !
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= null | true | false | intLiteral | charLiteral | stringLiteral
<Acceso> ::= ( <ExpParenOCasting> <Encadenado>
<Acceso> ::= <Primario> <Encadenado>
<ExpParenOCasting> ::= <ExpresionUnariaSinStatic> <ExpresionResto> )
<ExpParenOCasting> ::= idClase <RestoCastingORestoExpresion>
<RestoCastingORestoExpresion> ::= <RestoCasting> <AccesoEstaticoOPrimarioOExpParen>
<RestoCastingORestoExpresion> ::= <RestoAccesoEstatico> <ExpresionResto> )
<RestoCasting> ::= <GenExplicitaOVacio> )
<AccesoEstaticoOPrimarioOExpParen> ::= <AccesoEstatico> | <Primario> | <ExpresionParentizada>
<RestoAccesoEstatico> ::= . <AccesoVarOMetodo> <Encadenado>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoConstructor>
<Primario> ::= <AccesoVarOMetodo>
<AccesoEstatico> ::= idClase . <AccesoVarOMetodo> <Encadenado>
<AccesoThis> ::= this
<AccesoConstructor> ::= new idClase <GenericidadOVacio> <ArgsActuales>
<ExpresionParentizada> ::= ( <Expresion> )
<AccesoVarOMetodo> ::= idMetVar <ArgsActualesOVacio>
<ArgsActualesOVacio> ::= <ArgsActuales> | EOF
<ArgsActuales> ::= ( <ListaExpsOVacio> )
<ListaExpsOVacio> ::= <ListaExps> | EOF
<ListaExps> ::= <Expresion> <OtrosExps>
<OtrosExps> ::= , <ListaExps> | EOF
<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado> | EOF
<VarOMetodoEncadenado> ::= . idMetVar <ArgsActualesOVacio>