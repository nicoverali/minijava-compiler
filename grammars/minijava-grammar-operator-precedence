<Inicial> ::= <ListaClases>
<ListaClases> ::= <Clase> <OtrasClases>
<OtrasClases> ::= <ListaClases> | EOF
<Clase> ::= class idClase <GenExplicitaOVacio> <Herencia> { <ListaMiembros> }
<Herencia> ::= extends idClase <GenExplicitaOVacio> | EOF
<ListaMiembros> ::= <Miembro> <ListaMiembros> | EOF




<Miembro> ::= <Atributo> | <Metodo> | idClase <GenYListaAtrsOCons>
<GenYListaAtrsOCons> ::= <GenExplicitaOVacio> <ListaDecAtrs> ;
<GenYListaAtrsOCons> ::= <Constructor>
<Atributo> ::= <Visibilidad> <Tipo> <ListaDecAtrs> ;
<Atributo> ::= <TipoPrimitivo> <ListaDecAtrs> ;
<Metodo> ::= <FormaMetodo> <TipoMetodo> idMetVar <ArgsFormales> <Bloque>
<Constructor> ::= <ArgsFormales> <Bloque>
<Visibilidad> ::= public | private
<Tipo> ::= <TipoPrimitivo> | idClase <GenExplicitaOVacio>
<TipoPrimitivo> ::= boolean | char | int | String


<GenExplicitaOVacio> ::= < idClase > | EOF
<GenericidadOVacio> ::= < <RestoGenericidad> | EOF
<RestoGenericidad> ::= <GenRestoExplicito> | <GenRestoImplicito>
<GenRestoExplicito> ::= idClase >
<GenRestoImplicito> ::= >



<ListaDecAtrs> ::= idMetVar <AsignacionOVacio> <OtrosDecAtrs>
<OtrosDecAtrs> ::= , <ListaDecAtrs> | EOF
<FormaMetodo> ::= static | dynamic
<TipoMetodo> ::= <Tipo> | void
<ArgsFormales> ::= ( <ListaArgsFormalesOVacio> )
<ListaArgsFormalesOVacio> ::= <ListaArgsFormales> | EOF
<ListaArgsFormales> ::= <ArgFormal> <OtrosArgsFormales>
<OtrosArgsFormales> ::= , <ListaArgsFormales> | EOF
<ArgFormal> ::= <Tipo> idMetVar
<Bloque> ::= { <ListaSentencias> }
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | EOF
<Sentencia> ::= ;
<Sentencia> ::= <Acceso> <AsignacionOVacio> ;
<Sentencia> ::= <Tipo> <ListaDecVars> ;
<Sentencia> ::= if ( <Expresion> ) <Sentencia> <ElseOVacio>
<ElseOVacio> ::= else <Sentencia> | EOF
<Sentencia> ::= while ( <Expresion> ) <Sentencia>
<Sentencia> ::= <Bloque>
<Sentencia> ::= return <ExpresionOVacio> ;
<AsignacionOVacio> ::= <TipoDeAsignacion> <Expresion> | EOF
<TipoDeAsignacion> ::= = | += | -=
<ListaDecVars> ::= idMetVar <AsignacionOVacio> <OtrosDecVars>
<OtrosDecVars> ::= , <ListaDecVars> | EOF
<ExpresionOVacio> ::= <Expresion> | EOF
<Expresion> ::= <ExpNivel1>
<ExpresionUnaria> ::= <OperadorUnario> <Operando>
<ExpresionUnaria> ::= <Operando>
<OperadorUnario> ::= + | - | !
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= null | true | false | intLiteral | charLiteral | stringLiteral
<Acceso> ::= <Primario> <Encadenado>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoVarOMetodo>
<Primario> ::= <AccesoEstatico>
<Primario> ::= <AccesoConstructor>
<Primario> ::= ( <Expresion> )
<AccesoThis> ::= this
<AccesoVar> ::= idMetVar
<AccesoEstatico> ::= static idClase . <AccesoMetodo>
<AccesoConstructor> ::= new idClase <GenericidadOVacio> <ArgsActuales>
<AccesoMetodo> ::= idMetVar <ArgsActuales>
<AccesoVarOMetodo> ::= idMetVar <ArgsActualesOVacio>
<ArgsActualesOVacio> ::= <ArgsActuales> | EOF
<ArgsActuales> ::= ( <ListaExpsOVacio> )
<ListaExpsOVacio> ::= <ListaExps> | EOF
<ListaExps> ::= <Expresion> <OtrosExps>
<OtrosExps> ::= , <ListaExps> | EOF
<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado> | EOF
<VarOMetodoEncadenado> ::= . idMetVar <ArgsActualesOVacio>

<ExpNivel1> ::= <ExpNivel2> <ExpNivel1Resto>
<ExpNivel1Resto> ::= <OpNivel1> <ExpNivel2> <ExpNivel1Resto> | EOF
<OpNivel1> ::= == | !=

<ExpNivel2> ::= <ExpNivel3> <ExpNivel2Resto>
<ExpNivel2Resto> ::= <OpNivel2> <ExpNivel3> <ExpNivel2Resto> | EOF
<OpNivel2> ::= &&

<ExpNivel3> ::= <ExpNivel4> <ExpNivel3Resto>
<ExpNivel3Resto> ::= <OpNivel3> <ExpNivel4> <ExpNivel3Resto> | EOF
<OpNivel3> ::= ||

<ExpNivel4> ::= <ExpNivel5> <ExpNivel4Resto>
<ExpNivel4Resto> ::= <OpNivel4> <ExpNivel5> <ExpNivel4Resto> | EOF
<OpNivel4> ::= < | > | <= | >=

<ExpNivel5> ::= <ExpNivel6> <ExpNivel5Resto>
<ExpNivel5Resto> ::= <OpNivel5> <ExpNivel6> <ExpNivel5Resto> | EOF
<OpNivel5> ::= + | -

<ExpNivel6> ::= <ExpresionUnaria> <ExpNivel6Resto>
<ExpNivel6Resto> ::= <OpNivel6> <ExpresionUnaria> <ExpNivel6Resto> | EOF
<OpNivel6> ::= * | / | %