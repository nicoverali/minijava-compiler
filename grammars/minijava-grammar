<Inicial> ::= <ListaClases>
<ListaClases> ::= <Clase> <ListaClases> | <Clase>
<Clase> ::= class idClase <Herencia> { <ListaMiembros> }
<Herencia> ::= extends idClase | EOF
<ListaMiembros> ::= <Miembro> <ListaMiembros> | EOF
<Miembro> ::= <Atributo> | <Constructor> | <Metodo>
<Atributo> ::= <Visibilidad> <Tipo> <ListaDecAtrs> ;
<Metodo> ::= <FormaMetodo> <TipoMetodo> idMetVar <ArgsFormales> <Bloque>
<Constructor> ::= idClase <ArgsFormales> <Bloque>
<Visibilidad> ::= public | private
<Tipo> ::= <TipoPrimitivo> | idClase
<TipoPrimitivo> ::= boolean | char | int | String
<ListaDecAtrs> ::= idMetVar
<ListaDecAtrs> ::= idMetVar , <ListaDecAtrs>
<FormaMetodo> ::= static | dynamic
<TipoMetodo> ::= <Tipo> | void
<ArgsFormales> ::= ( <ListaArgsFormalesOVacio> )
<ListaArgsFormalesOVacio> ::= <ListaArgsFormales> | EOF
<ListaArgsFormales> ::= <ArgFormal>
<ListaArgsFormales> ::= <ArgFormal> , <ListaArgsFormales>
<ArgFormal> ::= <Tipo> idMetVar
<Bloque> ::= { <ListaSentencias> }
<ListaSentencias> ::= <Sentencia> <ListaSentencias> | EOF
<Sentencia> ::= ;
<Sentencia> ::= <Asignacion> ;
<Sentencia> ::= <Llamada> ;
<Sentencia> ::= <Tipo> <ListaDecVars> ;
<Sentencia> ::= if ( <Expresion> ) <Sentencia>
<Sentencia> ::= if ( <Expresion> ) <Sentencia> else <Sentencia>
<Sentencia> ::= while ( <Expresion> ) <Sentencia>
<Sentencia> ::= <Bloque>
<Sentencia> ::= return <ExpresionOVacio> ;
<Asignacion> ::= <Acceso> <TipoDeAsignacion> <Expresion>
<TipoDeAsignacion> ::= = | += | -=
<Llamada> ::= <Acceso>
<ListaDecVars> ::= idMetVar
<ListaDecVars> ::= idMetVar , <ListaDecVars>
<ExpresionOVacio> ::= <Expresion> | EOF
<Expresion> ::= <Expresion> <OperadorBinario> <ExpresionUnaria>
<Expresion> ::= <ExpresionUnaria>
<OperadorBinario> ::= || | && | == | != | < | > | <= | >= | + | - | * | / | %
<ExpresionUnaria> ::= <OperadorUnario> <Operando>
<ExpresionUnaria> ::= <Operando>
<OperadorUnario> ::= + | - | !
<Operando> ::= <Literal>
<Operando> ::= <Acceso>
<Literal> ::= null | true | false | intLiteral | charLiteral | stringLiteral
<Acceso> ::= <Primario> <Encadenado>
<Primario> ::= <AccesoThis>
<Primario> ::= <AccesoVar>
<Primario> ::= <AccesoEstatico>
<Primario> ::= <AccesoConstructor>
<Primario> ::= <AccesoMetodo>
<Primario> ::= ( <Expresion> )
<AccesoThis> ::= this
<AccesoVar> ::= idMetVar
<AccesoEstatico> ::= static idClase . <AccesoMetodo>
<AccesoConstructor> ::= new idClase <ArgsActuales>
<AccesoMetodo> ::= idMetVar <ArgsActuales>
<ArgsActuales> ::= ( <ListaExpsOVacio> )
<ListaExpsOVacio> ::= <ListaExps> | EOF
<ListaExps> ::= <Expresion>
<ListaExps> ::= <Expresion> , <ListaExps>
<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado> | EOF
<VarOMetodoEncadenado> ::= <VarEncadenada>
<VarOMetodoEncadenado> ::= <MetodoEncadenado>
<VarEncadenada> ::= . idMetVar
<MetodoEncadenado> ::= . idMetVar <ArgsActuales>